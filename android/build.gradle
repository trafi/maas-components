buildscript {
    ext {
        compose_version = '1.0.0-alpha07'
        kotlin_version = "1.4.10"
    }
    repositories {
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.0-alpha16'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.3.0"
    }
}

allprojects {
    version = '0.1.0-dev02-SNAPSHOT'

    repositories {
        google()
        jcenter()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            apply plugin: "org.jlleitschuh.gradle.ktlint"
            ktlint {
                version "0.39.0"
                android = true
            }
        }
    }
}

subprojects { project ->
    afterEvaluate {
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = '1.8'
                // avoid kotlin_module clash for projects with the same name, e.g. 'ui' & 'routes:ui"
                // https://discuss.kotlinlang.org/t/dealing-with-kotlin-module-conflict-when-building-apk-for-project-with-non-unique-project-names/11247
                if (project.hasProperty('PUBLISH_ARTIFACT_ID')) {
                    freeCompilerArgs += ['-module-name', "com.trafi.maas.$PUBLISH_ARTIFACT_ID"]
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

@SuppressWarnings('unused')
String resolveProperty(String propertyKey, String environmentVariable = null) {
    def gradleProp = project.findProperty(propertyKey)
    if (gradleProp) {
        return gradleProp
    }

    if (environmentVariable && System.hasProperty(environmentVariable)) {
        return System.getenv(environmentVariable)
    }

    def local = rootProject.file("local.properties")
    if (!local.canRead()) return null

    Properties localProperties = new Properties()
    local.withReader { localProperties.load(it) }
    return localProperties.getProperty(propertyKey)
}
