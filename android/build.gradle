buildscript {
    ext {
        compose_version = '1.0.0-alpha08'
        kotlin_version = "1.4.20"
    }
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0-alpha02'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.3.0"
        classpath "com.karumi:shot:5.5.0"
    }
}

allprojects {
    group = "com.trafi.maas"
    version = "0.1.0-dev03-SNAPSHOT"

    repositories {
        google()
        jcenter()
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            apply plugin: "org.jlleitschuh.gradle.ktlint"
            ktlint {
                version = "0.39.0"
                android = true
            }
        }
    }
}

subprojects { project ->
    afterEvaluate {
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach { task ->
            kotlinOptions {
                jvmTarget = '1.8'
                if (project.hasProperty('PUBLISH_ARTIFACT_ID')) {
                    // avoid kotlin_module clash for projects with the same name, e.g. 'ui' & 'routes:ui"
                    // https://discuss.kotlinlang.org/t/dealing-with-kotlin-module-conflict-when-building-apk-for-project-with-non-unique-project-names/11247
                    freeCompilerArgs += ['-module-name', "com.trafi.maas.$PUBLISH_ARTIFACT_ID"]
                    // enable explicit API mode, see https://youtrack.jetbrains.com/issue/KT-37652
                    if (!['test', 'Test'].any { task.name.contains(it) }) {
                        kotlinOptions.freeCompilerArgs += '-Xexplicit-api=strict'
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
